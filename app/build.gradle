import com.android.build.OutputFile

import java.text.SimpleDateFormat

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'realm-android'
apply plugin: 'com.google.firebase.firebase-crash'

def gitSha = 'git rev-parse --short HEAD'.execute([], project.rootDir).text.trim()
def buildTime = {
    def df = new SimpleDateFormat("HH:mm dd-MM-yyyy")
    df.setTimeZone(TimeZone.getTimeZone("GMT+2"))
    return df.format(new Date())
}()

def version_name = "1.0.5"
def version_code = 8
def appId = "com.sthagios.stopmotion"

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.2"



    defaultConfig {
        applicationId appId
        minSdkVersion 21
        targetSdkVersion 25
        versionCode version_code
        versionName version_name


        buildConfigField "String", "GIT_SHA", "\"$gitSha\""
        buildConfigField "String", "BUILD_TIME", "\"$buildTime\""

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        resValue "string", "fileprovider_authority", "${appId}.fileprovider"
    }

    productFlavors {
        dev {
            versionName "$version_name-Debug"
        }

        play {

        }
    }

    buildTypes {

        release {
            shrinkResources true
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }

        debug {
            applicationIdSuffix ".debug"
            resValue "string", "fileprovider_authority", "${appId}.fileprovider.debug"
        }
    }
    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }

    splits {
        abi {
            enable true
            reset()
            include 'armeabi-v7a', 'mips', 'x86'
            universalApk false
        }
    }
}

// map for the version code
ext.versionCodes = ['armeabi-v7a': 1, mips: 2, x86: 3]

android.applicationVariants.all { variant ->
    // assign different version code for each output
    variant.outputs.each { output ->
        output.versionCodeOverride =
                project.ext.versionCodes.get(output.getFilter(OutputFile.ABI)) * 1000000 +
                        android.defaultConfig.versionCode
    }
}



dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    testCompile 'junit:junit:4.12'
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"

    compile "com.android.support:appcompat-v7:$support_lib_version"
    compile "com.android.support:recyclerview-v7:$support_lib_version"
    compile "com.android.support:cardview-v7:$support_lib_version"

    compile "com.android.support:design:$support_lib_version"

    compile "com.google.firebase:firebase-core:$firebase_version"
    compile "com.google.firebase:firebase-crash:$firebase_version"
    compile "com.google.firebase:firebase-messaging:$firebase_version"

    //Rx
    compile 'io.reactivex:rxkotlin:0.55.0'
    compile 'io.reactivex:rxandroid:1.2.0'
    compile 'io.reactivex:rxjava:1.1.5'


    compile 'com.github.bumptech.glide:glide:3.7.0'
    compile 'com.afollestad.material-dialogs:core:0.8.5.9'
//    compile "com.android.support:support-v4:$support_lib_version"

    compile 'io.realm:android-adapters:1.2.2'

    compile 'com.github.wooplr:Spotlight:1.2.2'

    androidTestCompile 'com.android.support.test.espresso:espresso-core:2.2.2'
    androidTestCompile "com.android.support:support-annotations:$support_lib_version"
    androidTestCompile 'tools.fastlane:screengrab:0.3.2'

    androidTestCompile('com.android.support.test.espresso:espresso-contrib:2.0') {
        exclude group: 'com.android.support', module: 'appcompat'
        exclude group: 'com.android.support', module: 'support-v4'
        exclude module: 'recyclerview-v7'
    }

}


buildscript {
    ext.kotlin_version = '1.0.6'

    ext.support_lib_version = '25.1.0'
    ext.firebase_version = '10.0.1'
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "org.jetbrains.kotlin:kotlin-android-extensions:$kotlin_version"
    }
}
repositories {
    mavenCentral()
}
//Firebase
apply plugin: 'com.google.gms.google-services'
